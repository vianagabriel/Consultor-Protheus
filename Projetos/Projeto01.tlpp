#include 'totvs.ch'
#include 'Protheus.ch'
#include 'topconn.ch'

//@see https://tdn.totvs.com/display/tec/TDialog
//@see https://tdn.totvs.com/display/tec/TFolder
//@see https://tdn.totvs.com/display/tec/TSay
//@see https://tdn.totvs.com/display/tec/Date
//@see https://tdn.totvs.com/display/tec/TGroup
//@see https://tdn.totvs.com/display/tec/TRadMenu
//@see https://tdn.totvs.com/display/tec/TGet
//@see https://tdn.totvs.com/display/tec/TComboBox

User Function Projeto01()

	Private oDlg1
	Private cTituloJanela := "Projeto01 - Priemria tela em ADVPL Protheus"

    /* Variavel que recebe a funcao MsAdvSize()*/
	Private aSize := MsAdvSize()

	//Declarando as variaveis do folder
	Private oFolder, aFolder

    /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	   Variaveis referente aos objetos do FOLDER01(DADOS PESSOAIS)
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/
	Private oSayCod, oSayNome, oSayNomeReduzido, oSayDtNasc, oSayDDDCel, oSayCel, oSayCPF
	Private oGetCod, oGetNome, oGetNomeReduzido, oGetDtNasc, oGetDDDCel, oGetCel, oGetCPF
	Private cGetCod, cGetNome, cGetNomeReduzido, cGetCel, cGetDDDCel, cGetCPF
	Private dGetDataNasc
	Private oGroupSexo
	Private aSexo    := {"Masculino","Feminino"}
	Private nSexo    := 1
	Private oRadioSexo
	Private oTButtonAba1

	/* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	   Variaveis referente aos objetos do FOLDER02(ENDERECO)
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/
	Private oSayEndereco, oSayBairro, oSayImovel, oSayCEP, oSayEstado, oSayCidade
	Private oGetEndereco, oGetBairro, oGetCEP, oGetCidade
	Private cGetEndereco, cGetBairro, cGetCEP, cGetCidade
	Private aImoveis    := {"Casa","Apartamento","Outro"}
	Private cImovel     := aImoveis[3]
	Private aEstados    := {"AC", "AL", "AP", "AM", "BA", "CE", "DF", "ES", "GO", "MA", "MT", "MS", "MG", "PA", "PB", "PR", "PE", "PI", "RJ", "RN", "RS", "RO", "RR", "SC", "SP", "SE", "TO"}
	Private cEstado     := aEstados[1]
	Private oComboImovel, oComboEstado
	Private oTButtonAba2


    /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	   Variaveis referente aos objetos do FOLDER03(OUTROS)
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/

	Private oSayEstadoCivil
	Private aEstadoCivil := {"Solteiro","Casado","Separado","Divorciado","Viúvo"}
	Private cEstadoCivil := aEstadoCivil[1]
	Private oComboEstadoCivil
	Private oGroupStatusPCD
	Private aStatusPCD    := {"Sim","Não"}
	Private nStatusPCD    := 1
	Private oRadioStatusPCD
	Private oGroupLinguagens

	Private oCheckAdvpl
	Private oCheckJava
	Private oCheckCSharp
	Private oCheckPython

	Private lCheckAdvpl  := .F.
	Private lCheckJava   := .F.
	Private lCheckCSharp := .F.
	Private lCheckPython := .F.

	Private oTButton
	Private oTButton2
	Private oTButtonAba3


	Private oSayMultiGetObs
	Private oTMultiGetObs
	Private cMultiGetObs

    /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Variaveis que estão fora das abas
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/

	Private oButtonIncluir
	Private oButtonAlterar
	Private oButtonConsultar
	Private oButtonExcluir

	Private oBmpIncluir
	Private oBmpAlterar
	Private oBmpConsultar
	Private oBmpExcluir


	/* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	     JANELA PRINCIPAL  
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/
	oDlg1                := TDialog():New(aSize[7],aSize[1],aSize[6],aSize[5],cTituloJanela,,,,,CLR_BLACK,CLR_WHITE,,,.T.)

	aFolder              := {'Dados Pessoais','Endereço','Outros'}
	oFolder              := TFolder():New(0,aSize[1],aFolder,aFolder,oDlg1,,,,.T.,,aSize[5],200)

	/* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Primeira ABA --- DADOS PESSOAIS
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/
	oSayCod             := TSay():New(01,05,{||'Código'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetCod             := space(6)
	oGetCod             := TGet():New(10,05,{|u|if(PCount()==0,cGetCod,cGetCod:=u)},oFolder:aDialogs[1],30,009,"@ 999999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCod,,,, )
	oGetCod:cf3         := "SZ1"

	oSayNome             := TSay():New(26,05,{||'Nome'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetNome             := space(30)
	oGetNome             := TGet():New(36,05,{|u|if(PCount()==0,cGetNome,cGetNome:=u)},oFolder:aDialogs[1],096,009,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNome,,,, )

	oSayNomeReduzido     := TSay():New(26,110,{||'Nome Reduzido'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetNomeReduzido     := space(20)
	oGetNomeReduzido     := TGet():New(36,110,{|u|if(PCount()==0,cGetNomeReduzido,cGetNomeReduzido:=u)},oFolder:aDialogs[1],096,009,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNomeReduzido,,,, )

	oSayDtNasc           := TSay():New(26,220,{||'Data de nasciemnto'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	dGetDataNasc         := Date()
	oGetDtNasc           := TGet():New(36,220,{|u|if(PCount()==0,dGetDataNasc,dGetDataNasc:=u)},oFolder:aDialogs[1],096,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,,,,,.T.)

	oSayDDDCel           := TSay():New(26,320,{||'DDD'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetDDDCel           := space(4)
	oGetDDDCel           := TGet():New(36,320,{|u|if(PCount()==0,cGetDDDCel,cGetDDDCel:=u)},oFolder:aDialogs[1],20,009,"@E (99)",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetDDDCel,,,, )

	oSayCel              := TSay():New(26,360,{||'Celular'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetCel              := space(9)
	oGetCel              := TGet():New(36,360,{|u|if(PCount()==0,cGetCel,cGetCel:=u)},oFolder:aDialogs[1],40,009,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCel,,,, )

	oSayCPF              := TSay():New(58,05,{||'CPF'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetCPF              := space(14)
	oGetCPF              := TGet():New(67,05,{|u|if(PCount()==0,cGetCPF,cGetCPF:=u)},oFolder:aDialogs[1],096,009,"@E 999.999.999-99",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCPF,,,, )

	oGroupSexo           := TGroup():New(26,420,60,550,'Sexo',oFolder:aDialogs[1],,,.T.)
	oRadioSexo           := TRadMenu():New (36,425,aSexo,{|u|Iif (PCount()==0,nSexo,nSexo:=u)},oFolder:aDialogs[1],,,,,,,,60,20,,,,.T.)

	oTButtonAba1         := TButton():New(95,05, "Validar dados pessoais",oFolder:aDialogs[1],{|| MostrarDadosPessoais()}, 70,15,,,.F.,.T.,.F.,,.F.,,,.F. )

	/* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    SEGUNDA ABA ----- ENDERECO
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/
	oSayEndereco         := TSay():New(01,05,{||'Endereço'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetEndereco         := space(30)
	oGetEndereco         := TGet():New(10,05,{|u|if(PCount()==0,cGetEndereco,cGetEndereco:=u)},oFolder:aDialogs[2],096,009,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetEndereco,,,, )


	oSayBairro           := TSay():New(01,110,{||'Bairro'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetBairro           := space(20)
	oGetBairro           := TGet():New(10,110,{|u|if(PCount()==0,cGetBairro,cGetBairro:=u)},oFolder:aDialogs[2],070,009,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetBairro,,,, )

	oSayImovel           := TSay():New(01,190,{||'Tipo de Imóvel'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	oComboImovel         := TComboBox():New(10,190,{|u|if(PCount()>0,cImovel:=u,cImovel)},aImoveis,60,20,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cImovel')

	oSayCEP              := TSay():New(01,265,{||'CEP'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetCEP              := space(9)
	oGetCEP              := TGet():New(10,265,{|u|if(PCount()==0,cGetCEP,cGetCEP:=u)},oFolder:aDialogs[2],080,009,"@R 99999-999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCEP,,,, )

	oSayEstado           := TSay():New(25,05,{||'Estado'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	oComboEstado         := TComboBox():New(35,05,{|u|if(PCount()>0,cEstado:=u,cEstado)},aEstados,50,20,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cEstado')

	oSayCidade           := TSay():New(25,75,{||'Cidade'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	cGetCidade           := space(30)
	oGetCidade           := TGet():New(35,75,{|u|if(PCount()==0,cGetCidade,cGetCidade:=u)},oFolder:aDialogs[2],070,009,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCidade,,,, )

	oTButtonAba2         := TButton():New(75,05, "Validar endereço",oFolder:aDialogs[2],{|| MostrarDadosDoEndereco()}, 70,15,,,.F.,.T.,.F.,,.F.,,,.F. )

	/* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    TERCEIRA ABA ---- OUTROS
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/
	oSayEstadoCivil      := TSay():New(01,05,{||'Estado Civil'},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	oComboEstadoCivil    := TComboBox():New(10,05,{|u|if(PCount()>0,cEstadoCivil:=u,cEstadoCivil)},aEstadoCivil,80,20,oFolder:aDialogs[3],,,,,,.T.,,,,,,,,,'cEstadoCivil')

	oGroupStatusPCD      := TGroup():New(01,100,50,200,'Possui Deficiência?',oFolder:aDialogs[3],,,.T.)
	oRadioStatusPCD      := TRadMenu():New (10,105,aStatusPCD,{|u|Iif (PCount()==0,nStatusPCD,nStatusPCD:=u)},oFolder:aDialogs[03],,,,,,,,60,20,,,,.T.)

	oGroupLinguagens     := TGroup():New(01,220,55,340,'Linguagens que domina',oFolder:aDialogs[3],,,.T.)

	oCheckAdvpl          := TCheckBox():New(10,225,'ADVPL',{||lCheckAdvpl},oFolder:aDialogs[3],100,210,,{|| lCheckAdvpl := !lCheckAdvpl},,,,,,.T.,,,)
	oCheckJava           := TCheckBox():New(20,225,'Java',{||lCheckJava},oFolder:aDialogs[3],100,210,,{|| lCheckJava := !lCheckJava},,,,,,.T.,,,)
	oCheckCSharp         := TCheckBox():New(30,225,'CSharp',{||lCheckCSharp},oFolder:aDialogs[3],100,210,,{|| lCheckCSharp := !lCheckCSharp},,,,,,.T.,,,)
	oCheckPython         := TCheckBox():New(40,225,'Python',{||lCheckPython},oFolder:aDialogs[3],100,210,,{|| lCheckPython := !lCheckPython},,,,,,.T.,,,)

	oTButton             := TButton():New(15,280, "Todas",oFolder:aDialogs[3],{||lCheckAdvpl := .T., lCheckCSharp := .T., lCheckJava := .T., lCheckPython := .T.}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. )
	oTButton2            := TButton():New(30,280, "Nenhuma",oFolder:aDialogs[3],{||lCheckAdvpl := .F., lCheckCSharp := .F., lCheckJava := .F., lCheckPython := .F.}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. )

	oSayMultiGetObs      := TSay():New(01,360,{||'Observações'},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
	oTMultiGetObs        := tMultiget():new(10,360,{| u | if( pCount() > 0, cMultiGetObs := u, cMultiGetObs )},oFolder:aDialogs[3],200,80,,,,,,.T.)

	oTButtonAba3         := TButton():New(75,05, "Outras informações",oFolder:aDialogs[3],{||MostrarOutrosDados()}, 70,15,,,.F.,.T.,.F.,,.F.,,,.F. )


	/* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Elementos que estão fora do folder (estão na janela principal)
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/

	oBmpIncluir           := TBitmap():New(250, 30, 142, 142, NIL, "C:\TOTVS\Protheus\1212210\protheus_data\system\incluir.bmp", .T., oDlg1,{||}, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
	oButtonIncluir        := TButton():New(280,20, "Incluir",oDlg1,{||IncluirUsuarioSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )

	oBmpAlterar           := TBitmap():New(250, 90, 142, 142, NIL, "C:\TOTVS\Protheus\1212210\protheus_data\system\alterar.bmp", .T., oDlg1,{||}, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
	oButtonAlterar        := TButton():New(280,80, "Alterar",oDlg1,{||}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )

	oBmpConsultar         := TBitmap():New(250, 150, 142, 142, NIL, "C:\TOTVS\Protheus\1212210\protheus_data\system\consultar.bmp", .T., oDlg1,{||}, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
	oButtonConsultar      := TButton():New(280,140, "Consultar",oDlg1,{||ConsultarUsuarioSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )

	oBmpExcluir           := TBitmap():New(250,210, 142, 142, NIL, "C:\TOTVS\Protheus\1212210\protheus_data\system\excluir.bmp", .T., oDlg1,{||}, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
	oButtonExcluir        := TButton():New(280,200, "Excluir",oDlg1,{||}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. )

	/* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Ativacao do TDialog
	   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	*/
	oDlg1:Activate(,,,.T.)


Return



Static Function IncluirUsuarioSZ1()

	dbSelectArea("SZ1")// Selecionado a área de trabalho com o dbSelectArea

	dbGoTop()// Posiciona no primeiro registro da area de trabalho neste caso a SZ1

	// Verifica se alguns campos de cada aba obrigatórios estão vazios, pois está sendo presupondo que o usuário com verificando que da erro por falta de preenchimento preenchera todos os campos
	IF Empty(cGetCod) .OR. Empty(cGetNome) .Or. Empty(cGetEndereco) .OR. Empty(cEstadoCivil)
		FWAlertError("Campos obrigatórios não preenchidos, Preencha-os", "Erro")
		dbCloseArea()
		Return
	EndIf

    IF DbSeek(xFilial("SZ1")+cGetCod)// Verifica se dentro da tabela SZ1 já existe algum registro com este mesmo código que esta sendo recebido pela variavel cGetCod
        FWAlertError("Usuário já cadastrado com esse código", "Erro")//Mensagem de erro
        dbCloseArea()//Fechando a area de trabalho
        Return  
	EndIf		

	RecLock("SZ1",.T.)//  Travando a tabela para poder fazer inclusoes ou alteracoes, neste caso como o segundo parametro é .T. se trata de uma inclusao
  /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Dados Pessoais 
	 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  */
	SZ1->Z1_FILIAL   := xFilial("SZ1")// Ficará com 2 espaços vazios pois esta tabela é compartilhada
	SZ1->Z1_COD      := cGetCod
	SZ1->Z1_Nome     := cGetNome
	SZ1->Z1_NREDUZ   := cGetNomeReduzido
	SZ1->Z1_DTNASC   := dGetDataNasc //DToS transforma obj tipo data em string
	SZ1->Z1_DDDCEL   := StrTran(StrTran(cGetDDDCel,"(",""),")","") // O comando StrTran() faz um replace, ou seja substitui certo caracter por outro
	SZ1->Z1_CELULAR  := cGetCel
	SZ1->Z1_SEXO     := cValToChar(nSexo) // 1 = Masculino   2 = Feminino
	SZ1->Z1_CPF      :=	StrTran(StrTran(cGetCPF,".",""),"-","")
  /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Dados do Endereco 
     %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  */    
	SZ1->Z1_ENDEREC	 := cGetEndereco
	SZ1->Z1_BAIRRO   := cGetBairro
	SZ1->Z1_IMOVEL   := cImovel
	SZ1->Z1_CEP      := StrTran(cGetCEP,"-","")
	SZ1->Z1_ESTADO   := cEstado
	SZ1->Z1_CIDADE   := cGetCidade
  /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Dados Complementares 
	 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  */    
	SZ1->Z1_ESTIVI   := cEstadoCivil
	SZ1->Z1_DEFICIE  := cValToChar(nStatusPCD)
	SZ1->Z1_LADVPL   := IIF(lCheckAdvpl,"1","2")
	SZ1->Z1_LJAVA    := IIF(lCheckJava,"1","2")
	SZ1->Z1_LCSHARP  := IIF(lCheckCSharp,"1","2")
	SZ1->Z1_PYTHON   := IIF(lCheckPython,"1","2")
	SZ1->Z1_OBS      := cMultiGetObs
	MsUnlock()// Liberando a tabela
	dbCloseArea()

	msgInfo("Registro incluido com sucesso!!!","Sucesso")
Return


Static Function ConsultarUsuarioSZ1()
  
  dbSelectArea("SZ1")
  dbSetOrder(1)

  IF dbSeek(xFilial("SZ1")+cGetCod)
    msgInfo("Usuário localizado", "Sucesso")
    cGetCod          := SZ1->Z1_COD 
    cGetNome         := SZ1->Z1_Nome     
    cGetNomeReduzido := SZ1->Z1_NREDUZ
	dGetDataNasc     := SZ1->Z1_DTNASC   
	cGetDDDCel       := "("+SZ1->Z1_DDDCEL+")"    
    cGetCel          := SUBSTR(SZ1->Z1_CELULAR,1,1)+"-"+SUBSTR(SZ1->Z1_CELULAR,2,4)+"-"+SUBSTR(SZ1->Z1_CELULAR,6,4)
	nSexo            := Val(SZ1->Z1_SEXO)    
	cGetCPF          := SUBSTR(SZ1->Z1_CPF,1,3)+"."+SUBSTR(SZ1->Z1_CPF,4,3)+"."+SUBSTR(SZ1->Z1_CPF,7,3)+"-"+SUBSTR(SZ1->Z1_CPF,10,2)
  /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Dados do Endereco 
     %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  */    
    cGetEndereco     := SZ1->Z1_ENDEREC	
	cGetBairro       := SZ1->Z1_BAIRRO    
	cImovel          := SZ1->Z1_IMOVEL    
	cGetCEP          := SZ1->Z1_CEP
	cEstado          := SZ1->Z1_ESTADO   
    cGetCidade       :=	SZ1->Z1_CIDADE    
  /* %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	    Dados Complementares 
	 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  */    
    cEstadoCivil     := SZ1->Z1_ESTIVI    
	nStatusPCD       := Val(SZ1->Z1_DEFICIE)  
	lCheckAdvpl      := IIF(SZ1->Z1_LADVPL = "1",.T.,.F.)  
	lCheckJava       := IIF(SZ1->Z1_LJAVA = "1",.T.,.F.)
	lCheckCSharp     := IIF(SZ1->Z1_LCSHARP = "1",.T.,.F.)
	lCheckPython     := IIF(SZ1->Z1_PYTHON = "1",.T.,.F.)   
    cMultiGetObs     := SZ1->Z1_OBS 
  Else
    alert("Usuário não localizado")
  EndIF	

  dbCloseArea()


Return



Static Function MostrarDadosPessoais()
	msgInfo("Código: "+cGetCod+Chr(13)+Chr(10)+;
		"Nome: "+cGetNome+Chr(13)+Chr(10)+;
		"Nome Reduzido: "+cGetNomeReduzido+Chr(13)+Chr(10)+;
		"Data de Nascimento: "+DToC(dGetDataNasc)+Chr(13)+Chr(10)+;
		"Telefone: "+cGetDDDCel+"-"+cGetCel+Chr(13)+Chr(10)+;
		"Sexo: "+aSexo[nSexo]+Chr(13)+Chr(10)+;
		"CPF: "+cGetCPF,"Dados Pessoais")
Return

Static Function MostrarDadosDoEndereco()
	msgInfo("Endereço: "+cGetEndereco+Chr(13)+Chr(10)+;
		"Bairro: "+cGetBairro+Chr(13)+Chr(10)+;
		"Imovel: "+cImovel+Chr(13)+Chr(10)+;
		"Estado: "+cEstado+Chr(13)+Chr(10)+;
		"Cidade: "+cGetCidade,"Dados do endereço")
Return


Static Function MostrarOutrosDados()
	msgInfo("Estado Civil: "+cEstadoCivil+Chr(13)+Chr(10)+;
		"Possui Deficiência: "+aStatusPCD[nStatusPCD]+Chr(13)+Chr(10)+;
		"ADVPL: "+IIF(lCheckAdvpl,"Sim","Não")+Chr(13)+Chr(10)+;
		"Java: "+IIF(lCheckJava,"Sim","Não")+Chr(13)+Chr(10)+;
		"CSharp: "+IIF(lCheckCSharp,"Sim","Não")+Chr(13)+Chr(10)+;
		"Python: "+IIF(lCheckPython,"Sim","Não")+Chr(13)+Chr(10)+;
		"Observações: "+cMultiGetObs,"Dados do endereço")
Return
